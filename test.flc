FUNCTION_BLOCK agent

VAR_INPUT
	action : REAL;
	previousAction : REAL;
	power : REAL;
END_VAR

VAR_OUTPUT
    idea : REAL;
END_VAR

FUZZIFY action
    TERM moveUp := 0;
    TERM moveLeft:= 1;
    TERM moveRight := 2;
    TERM moveDown := 3;
    TERM search := 4;
	TERM pickUp := 5;
END_FUZZIFY

FUZZIFY previousAction
    TERM moveUp := 0;
    TERM moveLeft:= 1;
    TERM moveRight := 2;
    TERM moveDown := 3;
    TERM search := 4;
	TERM pickUp := 5;
END_FUZZIFY

FUZZIFY power
    TERM nonExistent := (0, 1) (0.2, 0);
	TERM average := gauss 0.5 0.25;
    TERM excellent := (0.8, 0) (1, 1);
END_FUZZIFY


DEFUZZIFY idea
    TERM terrible 	:= trian 0 1 2;
    TERM poor 		:= trian 1 2 3;
    TERM alright 	:= trian 2 3 4;
	TERM good 		:= trian 3 4 5;
	TERM excellent 	:= trian 4 5 6;
	
    METHOD : COG;
	
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK toMoveOrNotToMove
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    RULE 1 : IF action IS moveUp
                THEN idea IS alright;
    RULE 2 : IF action IS moveLeft 
                THEN idea IS alright;
    RULE 3 : IF action IS moveRight 
                THEN idea IS alright;
    RULE 4 : IF action IS moveDown 
                THEN idea IS alright;
				
    RULE 5 : IF action IS moveUp AND previousAction IS moveDown
                THEN idea IS terrible;
    RULE 6 : IF action IS moveLeft AND previousAction IS moveRight
                THEN idea IS terrible;
    RULE 7 : IF action IS moveRight AND previousAction IS moveLeft
                THEN idea IS terrible;
    RULE 8 : IF action IS moveDown AND previousAction IS moveUp
                THEN idea IS terrible;				
END_RULEBLOCK

RULEBLOCK toSearchOrNotToSearch
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    RULE 1 : IF action IS search AND power IS nonExistent
                THEN idea IS excellent;	
    RULE 2 : IF action IS search AND power IS average
                THEN idea IS alright;
	RULE 3 : IF action IS search AND power IS excellent
                THEN idea IS terrible;
				

    RULE 4 : IF action IS pickUp AND power IS nonExistent
                THEN idea IS excellent;	
    RULE 5 : IF action IS pickUp AND power IS average
                THEN idea IS alright;
	RULE 6 : IF action IS pickUp AND power IS excellent
                THEN idea IS terrible;
				
END_RULEBLOCK

END_FUNCTION_BLOCK